import os
env = Environment(ENV = os.environ)
os.environ['OMP_NUM_THREADS'] = '8'

from rsf.proj import *

# make velocity model
Flow('vel',None,
     '''
     spike nsp=4 n1=600 n2=700 d1=5 d2=10 k1=1,201,201,201 k2=1,1,1,1 l1=600,600,600,600 l2=700,700,700,700 mag=1500,200,200,200 p2=0,0,0.4,0.8 |
     put o1=0 o2=10 label1="z" label2="x" unit1="m" unit2="m"
     ''',stdin=-1)

# Smooth the velocity field used for migration
Flow('vel_smooth','vel',
     '''
     sfsmooth rect1=20 rect2=20
     ''')

# Density
Flow('den','vel','math output=1')
 
Flow('r_',None,'math n1=700 d1=10 o1=10 output=0')
Flow('s_',None,'math n1=1   d1=0  o1=0  output=0')
# receiver positions
Flow('zr','r_','math output=10')
Flow('xr','r_','math output="x1"')
Flow('rec',['xr','zr'],'''cat axis=2 space=n
     ${SOURCES[0]} ${SOURCES[1]} | transp
     ''', stdin=0)

# Wavelet
Flow('wav',None,
     '''
     spike mag=0.1 n1=4000 d1=0.001 k1=200 | 
     ricker1 frequency=20 
     ''')

Flow('wav_resample',None,
     '''
     spike mag=0.1 n1=1000 d1=0.004 k1=50 
     ''')

shots = []
nsx = 1
dsx = 1000.0
osx = 3500.0
for ishot in range(0,nsx):
     sx = osx + ishot*dsx
     shot = 'd_%d' % ishot
     shots.append(shot)
     # shot positions
     Flow('zs_%d' % ishot,'s_','math output=10')
     Flow('xs_%d' % ishot,'s_','math output=%d' % sx)
     Flow('rs_%d' % ishot,'s_','math output=1')
     Flow('src_%d' % ishot,['xs_%d' % ishot,'zs_%d' % ishot,'rs_%d' % ishot],'''
          cat axis=2 space=n
          ${SOURCES[0]} ${SOURCES[1]} ${SOURCES[2]} | transp
          ''', stdin=0)
     # offset
     Flow('offset_%d' % ishot,'xr','math output="input-%g"' % sx)
     # Finite-difference modeling
     Flow('d_fd_%d' % ishot,'wav vel den src_%d rec' % ishot,
          '''
          transp | 
          awefd2d 
          vel=${SOURCES[1]} den=${SOURCES[2]} 
          sou=${SOURCES[3]} rec=${SOURCES[4]} wfl=${TARGETS[1]} 
          verb=y free=n snap=n dabc=y jdata=1 nb=250 ompnth=8
          ''')
     Flow('d_%d' % ishot,'d_fd_%d offset_%d' % (ishot,ishot),
          '''
          transp |
          mutter offset=${SOURCES[1]} v0=1500 t0=0.25 half=n
          ''')

Flow('d',shots,
     '''
     cat axis=3 ${SOURCES[0:%d]} |
     put o3=%d d3=%d label3="sx" unit3=m label2="gx" unit2=m |
     math output=input*10e5 |
     window j1=4 |
     bandpass fmax=125 |
     envelope hilb=y phase=90
     ''' % (nsx,osx,dsx),stdin=0)

Flow('m','d vel_smooth wav_resample',
     '''
     mpiwem adj=y
     infile=${SOURCES[0]} vp=${SOURCES[1]} wav=${SOURCES[2]} 
     outfile=${TARGETS[0]}
     verbose=y
     nz=600 oz=0 dz=5 
     nhx=1 dhx=10 ohx=0
     npx=1 dpx=0.01 opx=0
     gz=10 sz=10
     fmin=0 fmax=50
     ''',stdin=0,stdout=-1)

Flow('d_fwd','m vel_smooth wav_resample',
     '''
     mpiwem adj=n
     infile=${SOURCES[0]} vp=${SOURCES[1]} wav=${SOURCES[2]} 
     outfile=${TARGETS[0]}
     verbose=y
     nz=600 oz=0 dz=5 
     nhx=1 dhx=10 ohx=0
     npx=1 dpx=0.01 opx=0
     nsx=1 dsx=100 osx=3500
     nt=1000 dt=0.004 ot=0
     gz=10 sz=10
     fmin=0 fmax=50
     ''',stdin=0,stdout=-1)

Flow('d_rand','d','noise var=10000 rep=y')
Flow('m_rand','m','noise var=10000 rep=y')

Flow('dot.test','d_rand m_rand vel_smooth wav_resample',
     '''
     dotwem
     d=${SOURCES[0]} m=${SOURCES[1]} v=${SOURCES[2]} wav=${SOURCES[3]} 
     verbose=y
     nz=600 oz=0 dz=5 
     nhx=1 dhx=10 ohx=0
     npx=1 dpx=0.01 opx=0
     nsx=1 dsx=100 osx=3500
     nt=1000 dt=0.004 ot=0
     gz=10 sz=10
     fmin=0 fmax=50
     ''',stdin=0)

Flow('m_inv misfit','d vel_smooth wav_resample',
     '''
     lswem
     niter=20
     d=${SOURCES[0]} vp=${SOURCES[1]} wav=${SOURCES[2]} 
     m=${TARGETS[0]} misfit=${TARGETS[1]}
     nz=600 oz=0 dz=5 
     nhx=1 dhx=10 ohx=0
     npx=1 dpx=0.01 opx=0
     nsx=1 dsx=100 osx=3500
     nt=1000 dt=0.004 ot=0
     gz=10 sz=10
     fmin=0 fmax=50
     ''',stdin=0,stdout=-1)

Flow('d_fwd2','m_inv vel_smooth wav_resample',
     '''
     mpiwem adj=n
     infile=${SOURCES[0]} vp=${SOURCES[1]} wav=${SOURCES[2]} 
     outfile=${TARGETS[0]}
     verbose=y
     nz=600 oz=0 dz=5 
     nhx=1 dhx=10 ohx=0
     npx=1 dpx=0.01 opx=0
     nsx=1 dsx=100 osx=3500
     nt=1000 dt=0.004 ot=0
     gz=10 sz=10
     fmin=0 fmax=50
     ''',stdin=0,stdout=-1)


Plot('vel','vel_smooth','grey color=j mean=y scalebar=y title="Smoothed Velocity" titlesz=20')
Plot('d','d','window n3=1 f3=1 | grey minval=-500 maxval=500 clip=500 scalebar=y color=e title="Data" titlesz=20')
Plot('d_fwd','d_fwd','window n3=1 f3=1 | grey scalebar=y color=e title="Forward" titlesz=20')
Plot('m','m','stack axis=3 | grey scalebar=y color=e title="Adjoint" titlesz=20')
Plot('m_inv','m_inv','stack axis=3 | grey scalebar=y color=e title="Inverse" titlesz=20')

Plot('d_zoom','d','window n2=1 f2=350 min1=1 max1=2 | wiggle pclip=100 title="d" titlesz=20 label2="" unit2="" g1num=0.1')
Plot('d_fwd_zoom','d_fwd','window n2=1 f2=350 min1=1 max1=2 | wiggle pclip=100 title="d_fwd" titlesz=20 label2="" unit2="" g1num=0.1')

Plot('d_fwd2','d_fwd2','window n3=1 f3=1 | grey minval=-500 maxval=500 clip=500 scalebar=y color=e title="Forward using m_inv" titlesz=20')
Plot('d_fwd2_zoom','d_fwd2','window n2=1 f2=350 min1=1 max1=2 | wiggle pclip=100 title="d_fwd2" titlesz=20 label2="" unit2="" g1num=0.1')

Plot('d_diff','d d_fwd2','math x=${SOURCES[0]} y=${SOURCES[1]} output="x-y" | window n3=1 f3=1 | grey minval=-1000 maxval=1000 clip=1000 scalebar=y color=e title="difference" titlesz=20')

Result('misfit','misfit','graph')

Result('all','vel d m d_fwd m_inv d_fwd2','SideBySideAniso')
Result('diff','d d_fwd2 d_diff','SideBySideAniso')
Result('zoom','d_zoom d_fwd_zoom d_fwd2_zoom','OverUnderAniso')
End()

